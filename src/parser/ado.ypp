%{
#include <cstdio>
#include <cstring>
#include <iostream>
#include <Rcpp.h>
#include "rstata.hpp"

void raise_condition(const std::string& msg, const std::string& type);

extern "C" int yylex(void);

void yyerror(std::vector<std::unique_ptr<BaseStataCmd>> *cmdlist_ptr, const char *str)
{
        raise_condition(str, "error");
}
 
int yywrap()
{
        return 1;
} 
 
%}

%union {
    char *str;
}

%parse-param {std::vector<std::unique_ptr<BaseStataCmd>> *cmdlist_ptr}
%define parse.error verbose
%start translation_unit

%token <str> EMBEDDED_R;

/* Literals of various fundamental types */
%token <str> NUMBER IDENT STRING_LITERAL UNQUOTED_FILEPATH
%token <str> NUMBER_MISSING STRING_MISSING

/* Flow control constructs */
%token <str> WHILE FORVALUES FOREACH

/* Type specifiers */
%token <str> BYTE INT LONG FLOAT DOUBLE STRING_TYPE_SPEC;

/* Multi-character infix operators */
%token <str> GT_OP LT_OP EQ_OP NE_OP OR_OP AND_OP

/* Formats */
%token <str> STRING_FORMAT DATETIME_FORMAT NUMBER_FORMAT

/* Command components and keywords */
%token <str> USING IF WITH IN
%token <str> LOCAL GLOBAL VARLIST NEWLIST NUMLIST OF
%token <str> _LITTLE_N _BIG_N _ALL _B _COEF _CONS _PI _PRED _RC _SKIP

/* Commands with unusual enough syntax they have to be parsed separately */
%token <str> QUIETLY CAPTURE NOISILY /* Prefix commands that can be used without a colon */
%token <str> SET LOG DUPLICATES MERGE

%token <str> NEWLINE

%%

/*
 *************************************************
 * The start symbol, a "command" and escaping to R
 *************************************************


shift/reduce conflicts appearing in relation to:
    the expression grammar (subtle issue probably)
    the prefix_cmd_list rule that can match empty
 */

translation_unit:
      block
    | translation_unit NEWLINE block
    ;

block:
      cmd
    | compound_cmd
    ;

compound_cmd:
      '{' '}'
    | '{' cmds '}'

cmds:
      cmd
    | cmds NEWLINE cmd
    ;

cmd:
      embedded_r_cmd
    | flow_control_statement
    | modifier_cmd_list nonmodifier_cmd_list
    ;

embedded_r_cmd:
    EMBEDDED_R
    ;

flow_control_statement:
      WHILE expression compound_cmd
    | FORVALUES IDENT '=' expression compound_cmd
    | foreach
    ;

foreach:
      FOREACH IDENT IN expression_list compound_cmd
    | FOREACH IDENT OF LOCAL IDENT compound_cmd
    | FOREACH IDENT OF GLOBAL IDENT compound_cmd
    | FOREACH IDENT OF VARLIST expression_list compound_cmd
    | FOREACH IDENT OF NEWLIST expression_list compound_cmd
    | FOREACH IDENT OF NUMLIST expression_list compound_cmd
    ;

nonmodifier_cmd_list:
      ':' nonmodifier_cmd
    | nonmodifier_cmd_list ':' nonmodifier_cmd
    ;

modifier_cmd:
      CAPTURE
    | QUIETLY
    | NOISILY
    ;

modifier_cmd_list:
      %empty
    | modifier_cmd
    | modifier_cmd_list modifier_cmd
    ;

/* FIXME - everything after IDENT should be optional */
nonmodifier_cmd:
    compound_cmd
    | special_cmd
    | IDENT expression_list if_clause in_clause weight_clause using_clause
    | IDENT expression_list if_clause in_clause weight_clause using_clause option_list
    ;

special_cmd:
      set_cmd
    | log_cmd
    | duplicates_cmd
    | merge_cmd
    ;

/*
 ************************************************************
 * The expression grammar. Note that varlists are expressions
 * syntactically, even if they're not semantically.
 ************************************************************
*/

assignment_operator:
    '='
    ;

unary_operator:
      '-'
    | '+'
    | '!'
    | '~'
    ;

power_operator:
    '^'
    ;

multiplication_operator:
      '*'
    | '/'
    | '%'
    ;

additive_operator:
      '+'
    | '-'
    ;

relational_operator:
      '<'
    | '>'
    | GT_OP
    | LT_OP
    ;

equality_operator:
      EQ_OP
    | NE_OP
    ;

literal:
      NUMBER
    | IDENT
    | STRING_LITERAL
    | STRING_MISSING
    | NUMBER_MISSING
    | UNQUOTED_FILEPATH
    ;

primary_expression:
      literal
    | '(' expression ')'
    ;

postfix_expression:
      primary_expression
    | postfix_expression '[' expression ']'
    | postfix_expression '(' ')'
    | postfix_expression '(' argument_expression_list ')'
    ;

argument_expression_list:
      logical_or_expression
    | argument_expression_list ',' logical_or_expression
    ;

power_expression:
      postfix_expression
    | postfix_expression power_operator postfix_expression
    ;

unary_expression:
      power_expression
    | unary_operator power_expression
    ;

multiplication_expression:
      unary_expression
    | multiplication_expression multiplication_operator unary_expression
    ;

additive_expression:
      multiplication_expression
    | additive_expression additive_operator multiplication_expression
    ;

relational_expression:
      additive_expression
    | relational_expression relational_operator additive_expression
    ;

equality_expression:
      relational_expression
    | equality_expression equality_operator relational_expression
    ;

logical_and_expression:
      equality_expression
    | logical_and_expression AND_OP equality_expression
    ;

logical_or_expression:
      logical_and_expression
    | logical_or_expression OR_OP logical_and_expression
    ;

expression:
      logical_or_expression
    | IDENT assignment_operator logical_or_expression
    ;

expression_list:
      expression
    | expression_list expression
    ;

/*
 **************************************************
 * Options and option lists
 **************************************************
 */

option_list:
      ',' option
    | ',' option_list option
    ;

option:
      IDENT
    | IDENT '(' option_arguments ')'
    ;

option_arguments:
      expression
    | option_arguments expression
    ;

/*
 **************************************************
 * The weight clause
 **************************************************
 */

weight_clause:
    '[' IDENT '=' expression ']';

/*
 **************************************************
 * The if clause
 **************************************************
 */

if_clause:
    IF expression
    ;

/*
 **************************************************
 * The in clause
 **************************************************
 */

in_clause:
    IN expression
    ;

/*
 **************************************************
 * The using clause
 **************************************************
 */

using_clause:
      USING STRING_LITERAL
    | USING UNQUOTED_FILEPATH
    | USING IDENT
    ;

/*
 ******************************
 * The "special" Stata commands
 ******************************
 */

set_cmd:
    SET IDENT expression
    ;

duplicates_cmd:
    DUPLICATES
    ;

log_cmd:
    LOG
    ;

merge_cmd:
    MERGE
    ;

%%

void
raise_condition(const std::string& msg, const std::string& type)
{
  Rcpp::List cond;
  cond["message"] = msg;
  cond["call"] = R_NilValue;
  cond.attr("class") = Rcpp::CharacterVector::create(type, "condition");
  Rcpp::Function stopper("stop");
  stopper(cond);
}

